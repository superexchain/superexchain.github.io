# SIP-721 标准
## 简要说明

非同质化（Non-Fungible Token，以下简称 NFT 或 NFTs）代币标准。

## 摘要


提案描述了智能合约中实现 NFT 的标准API。 标准提供了跟踪和转移NFTs的基本功能。

提案考虑了个人拥有和交易NFT的场景，以及委托给第三方（如：代理人/钱包/拍卖人，称为 “操作员”）的情况。

 NFT可以代表对数字或物理资产的所有权。 我们考虑了资产的多样性，并且我们知道您会想要代表更多：

- 实体财产 - 如：房屋，独特的艺术品
- 虚拟收藏品 - 如：小猫，收藏卡的独特图片
- “负”资产 - 如：贷款等

总的来说，所有房屋都是不同的，没有两只小猫是一样的。 NFT是*可区分的*，必须分别跟踪每个所有者。


## 动机

标准接口可以方便 钱包/代理/拍卖应用 与以太坊上的任何 NFT 进行交互。 提供简单的ERC-721智能合约以及跟踪*数量众多* NFT的合约。 其他应用将在下面讨论。


该标准的灵感来自ERC20代币标准，并建立自EIP-20创建以来的两年经验。 ERC20 无法跟踪NFT，因为每种资产都是不同的（不可替代的），而ERC20每个代币的数量都是相同的（可替代的）。

该标准与ERC20之间的差异在下面进行说明。


## 规范

**每个符合ERC-721的合同都必须实现 `ERC721` 和 `ERC165` 接口**（受以下“说明”约束）：


```solidity
pragma solidity ^0.4.20;

/// @title ERC-721非同质化代币标准
/// @dev See https://learnblockchain.cn/docs/eips/eip-721.html
///  Note: ERC-165 接口id 为 0x80ac58cd.
interface ERC721 /* is ERC165 */ {
    /// @dev 当任何NFT的所有权更改时（不管哪种方式），就会触发此事件。
    ///  包括在创建时（`from` == 0）和销毁时(`to` == 0), 合约创建时除外。
    event Transfer(address indexed _from, address indexed _to, uint256 indexed _tokenId);

    /// @dev 当更改或确认NFT的授权地址时触发。
    ///  零地址表示没有授权的地址。
    ///  发生 `Transfer` 事件时，同样表示该NFT的授权地址（如果有）被重置为“无”（零地址）。
    event Approval(address indexed _owner, address indexed _approved, uint256 indexed _tokenId);

    /// @dev 所有者启用或禁用操作员时触发。（操作员可管理所有者所持有的NFTs）
    event ApprovalForAll(address indexed _owner, address indexed _operator, bool _approved);

    /// @notice 统计所持有的NFTs数量
    /// @dev NFT 不能分配给零地址，查询零地址同样会异常
    /// @param _owner ： 待查地址
    /// @return 返回数量，也许是0
    function balanceOf(address _owner) external view returns (uint256);

    /// @notice 返回所有者
    /// @dev NFT 不能分配给零地址，查询零地址抛出异常
    /// @param _tokenId NFT 的id
    /// @return 返回所有者地址
    function ownerOf(uint256 _tokenId) external view returns (address);

    /// @notice 将NFT的所有权从一个地址转移到另一个地址
    /// @dev 如果`msg.sender` 不是当前的所有者（或授权者）抛出异常
    /// 如果 `_from` 不是所有者、`_to` 是零地址、`_tokenId` 不是有效id 均抛出异常。
    ///  当转移完成时，函数检查  `_to` 是否是合约，如果是，调用 `_to`的 `onERC721Received` 并且检查返回值是否是 `0x150b7a02` (即：`bytes4(keccak256("onERC721Received(address,address,uint256,bytes)"))`)  如果不是抛出异常。
    /// @param _from ：当前的所有者
    /// @param _to ：新的所有者
    /// @param _tokenId ：要转移的token id.
    /// @param data : 附加额外的参数（没有指定格式），传递给接收者。
    function safeTransferFrom(address _from, address _to, uint256 _tokenId, bytes data) external payable;

    /// @notice 将NFT的所有权从一个地址转移到另一个地址，功能同上，不带data参数。
    function safeTransferFrom(address _from, address _to, uint256 _tokenId) external payable;

    /// @notice 转移所有权 -- 调用者负责确认`_to`是否有能力接收NFTs，否则可能永久丢失。
    /// @dev 如果`msg.sender` 不是当前的所有者（或授权者、操作员）抛出异常
    /// 如果 `_from` 不是所有者、`_to` 是零地址、`_tokenId` 不是有效id 均抛出异常。
    function transferFrom(address _from, address _to, uint256 _tokenId) external payable;

    /// @notice 更改或确认NFT的授权地址
    /// @dev 零地址表示没有授权的地址。
    ///  如果`msg.sender` 不是当前的所有者或操作员
    /// @param _approved 新授权的控制者
    /// @param _tokenId ： token id
    function approve(address _approved, uint256 _tokenId) external payable;

    /// @notice 启用或禁用第三方（操作员）管理 `msg.sender` 所有资产
    /// @dev 触发 ApprovalForAll 事件，合约必须允许每个所有者可以有多个操作员。
    /// @param _operator 要添加到授权操作员列表中的地址
    /// @param _approved True 表示授权, false 表示撤销
    function setApprovalForAll(address _operator, bool _approved) external;

    /// @notice 获取单个NFT的授权地址
    /// @dev 如果 `_tokenId` 无效，抛出异常。
    /// @param _tokenId ：  token id
    /// @return 返回授权地址， 零地址表示没有。
    function getApproved(uint256 _tokenId) external view returns (address);

    /// @notice 查询一个地址是否是另一个地址的授权操作员
    /// @param _owner 所有者
    /// @param _operator 代表所有者的授权操作员
    function isApprovedForAll(address _owner, address _operator) external view returns (bool);
}

interface ERC165 {
    /// @notice 是否合约实现了接口
    /// @param interfaceID  ERC-165定义的接口id
    /// @dev 函数要少于  30,000 gas.
    /// @return 合约实现了 `interfaceID`（不为  0xffffffff）返回`true` ， 否则false.
    function supportsInterface(bytes4 interfaceID) external view returns (bool);
}
```

如果合约（应用）要接受NFT的安全转账，则必须实现**以下接口**。


```solidity
/// @dev 按 ERC-165 标准，接口id为 0x150b7a02.
interface ERC721TokenReceiver {
    /// @notice 处理接收NFT
    /// @dev ERC721智能合约在`transfer`完成后，在接收这地址上调用这个函数。
    /// 函数可以通过revert 拒绝接收。返回非`0x150b7a02` 也同样是拒绝接收。
    /// 注意: 调用这个函数的 msg.sender是ERC721的合约地址
    /// @param _operator ：调用 `safeTransferFrom` 函数的地址。
    /// @param _from ：之前的NFT拥有者
    /// @param _tokenId ： NFT token id
    /// @param _data ： 附加信息
    /// @return 正确处理时返回 `bytes4(keccak256("onERC721Received(address,address,uint256,bytes)"))`
    function onERC721Received(address _operator, address _from, uint256 _tokenId, bytes _data) external returns(bytes4);
}
```

以下元信息扩展是可选的（查看后面的“说明”部分），但是可以提供一些资产代表的信息以便查询。

```solidity
/// @title ERC-721非同质化代币标准, 可选元信息扩展
/// @dev See https://learnblockchain.cn/docs/eips/eip-721.html
///  Note: 按 ERC-165 标准，接口id为  0x5b5e139f.
interface ERC721Metadata /* is ERC721 */ {
    /// @notice NFTs 集合的名字
    function name() external view returns (string _name);

    /// @notice NFTs 缩写代号
    function symbol() external view returns (string _symbol);

    /// @notice 一个给定资产的唯一的统一资源标识符(URI)
    /// @dev 如果 `_tokenId` 无效，抛出异常. URIs在 RFC 3986 定义，
    /// URI 也许指向一个 符合 "ERC721 元数据 JSON Schema" 的 JSON 文件
    function tokenURI(uint256 _tokenId) external view returns (string);
}
```

以下是 "ERC721 元数据 JSON Schema" 描述：

```json
{
    "title": "Asset Metadata",
    "type": "object",
    "properties": {
        "name": {
            "type": "string",
            "description": "指示NFT代表什么"
        },
        "description": {
            "type": "string",
            "description": "描述NFT 代表的资产"
        },
        "image": {
            "type": "string",
            "description": "指向NFT表示资产的资源的URI（MIME 类型为 image/*） ， 可以考虑宽度在320到1080像素之间，宽高比在1.91:1到4:5之间的图像。
        }
    }
}
```

以下枚举扩展信息是可选的（查看后面的“说明”部分），但是可以提供NFTs的完整列表，以便NFT可被发现。


```solidity
/// @title ERC-721非同质化代币标准枚举扩展信息
/// @dev See https://learnblockchain.cn/docs/eips/eip-721.html
///  Note: 按 ERC-165 标准，接口id为 0x780e9d63.
interface ERC721Enumerable /* is ERC721 */ {
    /// @notice  NFTs 计数
    /// @return  返回合约有效跟踪（所有者不为零地址）的 NFT数量
    function totalSupply() external view returns (uint256);

    /// @notice 枚举索引NFT
    /// @dev 如果 `_index` >= `totalSupply()` 则抛出异常
    /// @param _index 小于 `totalSupply()`的索引号
    /// @return 对应的token id（标准不指定排序方式)
    function tokenByIndex(uint256 _index) external view returns (uint256);

    /// @notice 枚举索引某个所有者的 NFTs
    /// @dev  如果 `_index` >= `balanceOf(_owner)` 或 `_owner` 是零地址，抛出异常
    /// @param _owner 查询的所有者地址
    /// @param _index 小于 `balanceOf(_owner)` 的索引号
    /// @return 对应的token id （标准不指定排序方式)
    function tokenOfOwnerByIndex(address _owner, uint256 _index) external view returns (uint256);
}
```

### 说明

Solidity 0.4.20 接口语法不足以表达 ERC-721 标准文档，ERC-721标准的合约还必须遵守以下规则:

- Solidity issue #3412: 上面的接口为每个函数包括明确的可变性声明。 可变性声明从弱到强依次为：`payable`，隐含不可支付， `view` 和 `pure`。 实现必须满足此接口中的可变性声明，或使用更强的可变性声明。 例如，接口中的 `payable` 函数可以在合约中实现为不可支付（即未指定）。
- Solidity issue #3419: 实现 `ERC721Metadata` 和 `ERC721Enumerable` 接口同时也需要实现 `ERC721`， ERC-721 要求实现ERC-165接口。
- Solidity issue #2330: 标记为 `external` 的函数也可以使用 `public` 可见性。


## 原理阐述

许多的智能合约提案依赖于跟踪可区分的资产。 如 Decentraland的LAND，CryptoPunk中的eponymous punks，以及使用DMarket或EnjinCoin等系统的游戏内物品。 将来的用途包括跟踪现实世界资产，例如房地产（如Ubitquity或Propy等公司所设想的）。在这些情况下，这些项目不能像账本中的数字那样“集中”在一起，而是每个资产必须单独和原子地跟踪所有权。不管资产的性质如何，如果具有允许跨职能资产管理和销售平台的标准化接口，那么生态系统将变得更加强大。

**"NFT" 选词**

"NFT" 几乎是所有接受调查的人都满意的词，并且广泛适用于可区分的数字资产领域。

*考虑的替代方案：可区分的资产，所有权，代币（通证），资产，权益，票据*

**NFT 身份ID**

每个NFT都由ERC-721智能合约内部的唯一 `uint256` ID标识。 该识别码在整个协议期内均不得更改。 `(合约地址, tokenId)` 对将成为以太坊链上特定资产的全球唯一且完全合格的标识符。 尽管某些ERC-721智能合约可能会方便地以ID为0起始并为每个新的NFT加1，但调用者不得假设ID号具有任何特定的模式，并且必须将ID视为“黑匣子” 。 另请注意，NFT可能会变得无效（被销毁）。 请参阅支持的枚举接口的枚举功能。


由于UUIDs和sha3哈希可以直接转换为 `uint256` ，因此使用 `uint256` 可实现更广泛的应用。


**代币转移（或称转账）**

ERC-721 标准有两个转移函数`safeTransferFrom` (重载了带 data 和不带 data 参数 二种函数形式) 及 `transferFrom`，转移可由一下角色发起：

- NFT的所有者
- NFT的被授权（approved）地址
- NFT当前所有者授权的（authorized）操作员

此外, 授权的操作员也可以为NFT设置授权（approved）地址，这可以为钱包、经纪人和拍卖应用提供一套强有力的工具，方便快速使用*大量*的NFT。



转移方法仅仅列出了特定条件下需要抛出异常的情况，我们自己的实现也可以在其他情况下抛出异常， 这可以实现一些有趣效果：

- **如果合约暂定，可以禁用转账** — 如 CryptoKitties 合约（611行）
- **将接收NFT的某些地址列入黑名单** — 如 CryptoKitties 合约（565, 566行）
- **禁用非安全的转账** — 除非 `_to` 为 `msg.sender` 或 `countOf(_to)` 为非零 或之前是非零（这些情况是安全的） ， 否则 `transferFrom` 抛出异常。
- **向交易双方收取费用** — 从零地址向非零地址授权（ `approve` ）可以要求支付费用，还原到（ `approve`） 零地址时退款；调用任何transfer时要求支付费用；要求 transfer 参数 `_to` 等于 `msg.sender` ；要求 transfer 参数 `_to` 是 NFT 授权（approved）的地址
- **只读的NFT注册表** — 使用函数 `unsafeTransfer`, `transferFrom`, `approve` 以及 `setApprovalForAll` 时都抛出异常。